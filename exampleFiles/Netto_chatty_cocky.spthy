//run: tamarin-prover --heuristic=o --oraclename=oracle.py --stop-on-trace=SEQDFS --prove
//
//==============================================================================
//summary of summaries:
//
//analyzed: Netto.spthy
//
//  chatty (all-traces): verified (52 steps)
//  readA (all-traces): verified (33 steps)
//  lookA (all-traces): verified (36 steps)
//  sendcashNoAtt (exists-trace): verified (9 steps)
//  cashforvoucherNetto (all-traces): verified (4 steps)
//  cashforcontainerNetto (all-traces): verified (4 steps)
//  cashforcontainerCustNetto (all-traces): verified (1071 steps)
//  cashforpurchaseNetto (all-traces): verified (1878 steps)
//  cashforpurchaseCustNetto (all-traces): verified (116 steps)
//  cashforpurchaseCustNettoAttack (all-traces): falsified - found trace (28 steps)
//  cashforpurchaseNettoAttack (all-traces): falsified - found trace (28 steps)
//  cashforcontainerCustNettoAttack (all-traces): falsified - found trace (38 steps)
//
//==============================================================================
//tamarin-prover --heuristic=o --oraclename=oracle.py --stop-on-trace=SEQDFS    299.08s user 179.35s system 500% cpu 1:35.64 total

theory Netto 

begin

functions: receipt/1, 
object/1,
look/1,
get/1

equations: get(object(a))=a,
           look(receipt(a))=a

//RULES FOR VENDING MACHINE 
rule 1Purchase:
    [Fr(~cont_i)]
    --[Type($Cu, 'customer'),
       Purchase($S,$Cu,~cont_i)]->
    [ !Container(object(~cont_i)),
      Out_O($S,$Cu,object(~cont_i))
    ]


rule 3Output:
    [!Container(object(cont_i)),
    Fr(~barcode_i),
    In_O($Cu, $V, object(cont_i))]
    --[SendBarcode(~barcode_i),GetCont($V,$Cu,cont_i),Type($Cu,
    'customer'),Type($V, 'vending')]->
    [Out_O($V, $Cu, receipt(~barcode_i)),
    Info(~barcode_i,cont_i)]



//RULES FOR THE CASHIER
rule 5Cash:
    [In_O($Cu,$Ca, receipt(barcode_i)), Info(barcode_i,cont_i)]
    --[Cash($Ca, $Cu, cont_i, barcode_i),
    Type($Cu, 'customer'),Type($Ca, 'cashier')]->
    []


//RULES FOR THE CUSTOMER
rule 2Return:
    [In_O($S,$Cu,object(cont_i))]
    --[Type($Cu, 'customer'), Type($V, 'vending')]->
    [Out_O($Cu,$V,object(cont_i))]

rule 4Hand:
    [In_O($V,$Cu,receipt(barcode_i))]
    --[Type($Ca, 'cashier'),Type($Cu, 'customer'), Type($V,
    'vending')]->
    [Out_O($Cu,$Ca,receipt(barcode_i))]



//RULES FOR CHANNELS
rule ChanOut_I:
   [ Out_I($A, $B, x) ]
  --[ ChanOut_I($A, $B, x), Neq($A,$B)]->
   [ !Sec_I($A, $B, x) ]

rule ChanIn_I:
   [ !Sec_I($A, $B, x) ]
  --[ ChanIn_I($A, $B, x), Neq($A,$B)]->
   [ In_I($A, $B, x)]

rule ChanIn_O:
   [ Pos($A, $B, x) ]
  --[ ChanIn_O($A, $B, x), Neq($A,$B)]->
   [ In_O($A, $B, x) ]

rule ChanOut_OO:
    let x=object(y)
    in
   [ Out_O($A, $B, x) ]
  --[ ChanIn_O($A, $B, x), Neq($A,$B)]->
   [ Pos($A, $B, x) , Look($B,y)]

rule ChanOut_OP:
    let x=receipt(y)
    in
   [ Out_O($A, $B, x) ]
  --[ ChanIn_O($A, $B, x), Neq($A,$B), NoType($B,'vending')]->
   [ Pos($A, $B, x) , Read($B,y)]

//RULES FOR HUMAN KNOWLEDGE
rule Receive_HK:
    [ GotInfo($H, x) ]
  	--[Once($H,x)]-> 
    [ !HK($H, x) ]

rule Look:
    [Look($A, x)]
    --[Human($A)]->
    [GotInfo($A,x)]

rule Read:
    [Read($A, x)]
    --[Human($A)]->
    [GotInfo($A,x)]

//Chatty
rule Chatty [color=#FF0000]:
   [ !HK($H,  x)]
  --[ Chatty($H,  x)]->
   [ Out(x) ]

//Cocky give/hand 
rule Cocky [color=#FF0000]:
    [ Pos($A,$B, x) ]
  --[  Cocky($B,x), Neq($A,$B), Neq($A,$C), Neq($B,$C), Human($B), Human($C),Once(x,x)]->
   [ In_OA($B,$C,x) ]

//Cocky get 
rule CockyGet:
    [ Out_OA($A, $B, x) ]
  --[ Neq($A,$B), Human($A)]->
   [ Out_O($A,$B, x) ]

// Rules for dealing with objects and receipts 
rule GiveA:
[PosUA($Att,x)]
--[Human($Att)]->
[Out_OA($Att,$B,x)]

rule GetA:
[In_OA($A,$Att,x)]
--[Human($Att)]->
[PosDA($Att,x)]

rule LookA:
let x=object(y)
in
[PosDA($Att,x)]
--[LookA(y),Human($Att) ]->
[Out(y), PosUA($Att,x)]

rule ReadA:
let x=receipt(y)
in
[PosDA($Att,x)]
--[ReadA(y),Human($Att)]->
[Out(y), PosUA($Att,x)]

//RESTRICTIONS

restriction inequality:
"
    All x #i. Neq(x,x) @#i ==> F 
"

restriction type_distinct:
  "(All x t1 t2 #i #j. (Type(x,t1)@i & Type(x,t2)@j) ==> (t1 = t2))"

  
restriction once:
"All x y #i #j. Once(x,y)@i & Once(x,y)@j ==> #i=#j"

restriction human:
"All x #i. Human(x)@i ==> (Ex #j. Type(x, 'customer')@j & j < i) | 
(Ex #j. Type(x,'cashier')@j & j < i)"

restriction notype:
"All x y #i. NoType(x, y)@i ==> not(Ex #j. Type(x,y)@j)"

//LEMMAS 
lemma chatty[sources]:
"All h x #i. Chatty(h,x)@i ==>
(Ex #j. KU(x)@j & j<i) | 
(Ex s c #j. Purchase(s,c,x)@j & j<i) | 
(Ex #j. SendBarcode(x)@j & j<i) " 

lemma readA[sources]:
"All x #i. ReadA(x)@i ==>
(Ex #j. KU(x)@j & j<i) | 
(Ex #j. SendBarcode(x)@j & j<i) " 

lemma lookA[sources]:
"All  x #i. LookA(x)@i ==>
(Ex #j. KU(x)@j & j<i) | 
(Ex h c #j. Purchase(h,c,x)@j & j<i) | 
(Ex #j. SendBarcode(x)@j & j<i) " 


//Sanity
lemma sendcashNoAtt:
exists-trace "Ex  a b m n #i. Cash(a,b,m,n)@i & 
not (Ex  x #k. PrintA(x)@k) & 
not (Ex  x #k. BuildA(x)@k) & 
not (Ex h x #k. Chatty(h,x)@k) & 
not (Ex h x #k. Cocky(h,x)@k) 
"


//SECURITY PROPERTIES
lemma cashforvoucherNetto:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i 
==> 
 Ex #j . 
 SendBarcode(bar)@j & j<i  &
 (not(Ex a2 b2 #i2. Cash(a2,b2,c,bar)@i2 & not(#i=#i2)) |
 Ex #j2. SendBarcode(bar)@j2 & not(#j=#j2))"


lemma cashforcontainerNetto:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i  
==>
 Ex v  cu1  #j . 
 GetCont(v,cu1,c)@j & j<i  &
 (not(Ex ca1 cu2  bar2 #i2. Cash(ca1,cu2,c,bar2)@i2 & not(#i=#i2)) |
 Ex v1 cu3 #j2. GetCont(v1,cu3,c)@j2 & not(#j=#j2))"


lemma cashforcontainerCustNetto:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i  &
((not (Ex h x #k. Chatty(h,x)@k)  &  
not (Ex h x #k. Cocky(h,x)@k)) | 
(not (Ex  x #k. PrintA(x)@k)  &
not (Ex h x #k. Cocky(h,x)@k)) ) 
==>
 Ex #j v. 
 GetCont(v,cu,c)@j & j<i  &
 (not(Ex ca1 bar2 #i2. Cash(ca1,cu,c,bar2)@i2 & not(#i=#i2)) |
 Ex v1 #j2. GetCont(v1,cu,c)@j2 & not(#j=#j2))"


lemma cashforpurchaseNetto:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i &
( not (Ex  x #k. BuildA(x)@k) | 
(not (Ex h x #k. Chatty(h,x)@k)    &
not (Ex h x #k. Cocky(h,x)@k) ) )
==>
 Ex cu1 s #j . 
 Purchase(s,cu1,c)@j & j<i  &
 (not(Ex ca1 cu2  bar2 #i2. Cash(ca1,cu2,c,bar2)@i2 & not(#i=#i2)) |
 Ex s1 cu3 #j2. Purchase(s1,cu3,c)@j2 & not(#j=#j2))"


lemma cashforpurchaseCustNetto:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i &
((not (Ex h x #k. Chatty(h,x)@k)  &  
not (Ex h x #k. Cocky(h,x)@k) ) |
(
not (Ex  x #k. PrintA(x)@k) & 
not (Ex h x #k. Cocky(h,x)@k) &
not (Ex  x #k. BuildA(x)@k))) 
==>
 Ex s #j . 
 Purchase(s,cu,c)@j & j<i  &
 (not(Ex ca1 bar2 #i2. Cash(ca1,cu,c,bar2)@i2 & not(#i=#i2)) |
 Ex s1 #j2. Purchase(s1,cu,c)@j2 & not(#j=#j2))"


//Below are the attack traces for the properties failing against the strongest
//threat model combination

lemma cashforpurchaseCustNettoAttack:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i 
==>
 Ex s #j . 
 Purchase(s,cu,c)@j & j<i  &
 (not(Ex ca1 bar2 #i2. Cash(ca1,cu,c,bar2)@i2 & not(#i=#i2)) |
 Ex s1 #j2. Purchase(s1,cu,c)@j2 & not(#j=#j2))"

lemma cashforpurchaseNettoAttack:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i 
==>
 Ex s cu1 #j . 
 Purchase(s,cu1,c)@j & j<i  &
 (not(Ex ca1 cu2  bar2 #i2. Cash(ca1,cu2,c,bar2)@i2 & not(#i=#i2)) |
 Ex s1 cu3 #j2. Purchase(s1,cu3,c)@j2 & not(#j=#j2))"

lemma cashforcontainerCustNettoAttack:
"All ca cu c bar #i . Cash(ca,cu,c,bar)@i  
==>
 Ex #j v. 
 GetCont(v,cu,c)@j & j<i  &
 (not(Ex ca1 bar2 #i2. Cash(ca1,cu,c,bar2)@i2 & not(#i=#i2)) |
 Ex v1 #j2. GetCont(v1,cu,c)@j2 & not(#j=#j2))"
end


